plugins {
    id 'java-library'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.jaehyun'
version = '0.0.1-SNAPSHOT'
description = 'core module'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

ext {
    queryDslVersion = "5.1.0"
}

dependencies {

    //H2 메모리 DB -> 테스트용
    testImplementation 'com.h2database:h2'
    // === JPA & DB ===
    api 'org.springframework.boot:spring-boot-starter-data-jpa'
    api 'org.postgresql:postgresql'
    api "org.hibernate.orm:hibernate-spatial:6.6.33.Final"
    api 'org.locationtech.jts:jts-core:1.19.0'
    api 'org.geolatte:geolatte-geom:1.9.1'
    //redis - token 저장
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    //redisson - 분산 락 설정
    implementation 'org.redisson:redisson-spring-boot-starter:3.24.3'
    // === Test (JUnit 5.11 + Gradle 8.14 호환) ===
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage'
    }
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.3'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.11.3'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.11.3'

    // === QueryDSL ===
    api "com.querydsl:querydsl-jpa:${queryDslVersion}:jakarta"
    annotationProcessor "com.querydsl:querydsl-apt:${queryDslVersion}:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"

    // === Lombok ===
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
}

def generatedDir = file("build/generated/querydsl")

sourceSets {
    main {
        java {
            srcDir generatedDir
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.annotationProcessorGeneratedSourcesDirectory = generatedDir
}

tasks.named('test') {
    useJUnitPlatform()
}
